
"""
    PublicKey

Stores a public key generated by a `KeyGenerator` instance.

See also: [`KeyGenerator`](@ref)
"""
mutable struct PublicKey <: SEALObject
  handle::Ptr{Cvoid}

  function PublicKey()
    handleref = Ref{Ptr{Cvoid}}(C_NULL)
    retval = ccall((:PublicKey_Create1, libsealc), Clong,
                   (Ref{Ptr{Cvoid}},),
                   handleref)
    @check_return_value retval
    return PublicKey(handleref[])
  end

  function PublicKey(handle::Ptr{Cvoid})
    x = new(handle)
    finalizer(x) do x
      # @async println("Finalizing $x at line $(@__LINE__).")
      ccall((:PublicKey_Destroy, libsealc), Clong, (Ptr{Cvoid},), x)
    end
    return x
  end
end

